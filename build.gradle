import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'ru.megadevelopers'
version = '1.0'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.hibernate:hibernate-core:4.3.4.Final'
	testCompile 'junit:junit:4.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
				packaging 'jar'
				name 'postgres-hibernate-mapper'
				description 'postgre-hibernate-mapper is the small project to handle default hibernate id generation strategy.'
				url 'https://github.com/allnightlong/postgres-hibernate-mapper'

				scm {
					url 'scm:git@github.com:allnightlong/postgres-hibernate-mapper.git'
					connection 'scm:git@github.com:allnightlong/postgres-hibernate-mapper.git'
					developerConnection 'scm:git@github.com:allnightlong/postgres-hibernate-mapper.git'
				}

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}

				developers {
					developer {
						id 'allnightlong'
						name 'Tigran Kavanosyan'
					}
				}
			}
		}
	}
}